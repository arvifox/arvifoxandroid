buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.17"
    }
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

//apply plugin: 'androidx.navigation.safeargs'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'org.jetbrains.dokka-android'

// Creates a variable called keystorePropertiesFile, and initializes it to the
// keys.properties file.
def keystorePropertiesFile = rootProject.file("mykeys.properties")
// Initializes a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Loads the keys.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        configrelease {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 30
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId "com.arvifox.arvi"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        manifestPlaceholders = [googleGeoApiKey: keystoreProperties['googleGeoApiKey']]
        resConfigs "en", "fr", "ru", "en_XA", "ar_XB"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }
    buildTypes {
        debug {
            pseudoLocalesEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.configrelease
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "ARVI_API_URL", "\"" + keystoreProperties['arviApiUrl'] + "\""
            variant.buildConfigField "String", "FLICKR_KEY", "\"" + keystoreProperties['flickrKey'] + "\""
            variant.buildConfigField "String", "TMDB_KEY", "\"" + keystoreProperties['tmdb'] + "\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        main.res.srcDirs += ['src/main/res-some']
        main.jni.srcDirs += ['src/main/jniLibs']
        main.jniLibs.srcDirs += ['src/main/jniLibs']
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
//    ndk {
    /*
             * Sceneform is available for the following ABIs: arm64-v8a, armv7a,
             * x86_64 and x86. This sample app enables arm64-v8a to run on
             * devices and x86 to run on the emulator. Your application should
             * list the ABIs most appropriate to minimize APK size (arm64-v8a recommended).
             */
//        abiFilters 'arm64-v8a', 'x86'
//    }
    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.annotation:annotation:1.2.0'

    //test
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.work:work-testing:2.5.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //multidex
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'androidx.palette:palette-ktx:1.0.0'

    //arch
    implementation "androidx.work:work-runtime-ktx:2.5.0"
    implementation "androidx.work:work-rxjava2:2.5.0"
    androidTestImplementation "androidx.work:work-testing:2.5.0"

    //lifecycler
    def lifecycle_version = "2.3.1"
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    //kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version" // For Kotlin use kapt instead of annotationProcessor
    // alternately - if using Java8, use the following instead of lifecycle-compiler
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    // optional - ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"
    // optional - Test helpers for LiveData
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.3.1'
    // view model scope
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    // lifecycle scope
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    // livedata scope
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"

    def nav_version = "2.3.5"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // room

    def room_version = "2.2.6"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // For Kotlin use kapt instead of annotationProcessor
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"
    // optional - RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"
    // optional - Guava support for Room, including Optional and ListenableFuture
    //implementation "androidx.room:room-guava:$room_version"
    // Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    //room end

    def camerax_version = "1.1.0-alpha03"
    // CameraX core library using camera2 implementation
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation "androidx.camera:camera-view:1.0.0-alpha23"

    //moshi
    implementation 'com.squareup.moshi:moshi-kotlin:1.9.2'
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.9.2"

    //implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.1.0'

    //retrofit okhttp
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.7.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.2'
    implementation 'com.squareup.retrofit2:retrofit-mock:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.1'
    implementation 'com.squareup.okhttp3:okhttp:4.7.1'
    implementation "com.squareup.okhttp3:okhttp-urlconnection:4.7.1"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    // AR core, Sceneform
    implementation 'com.google.ar:core:1.23.0'
    implementation("com.google.ar.sceneform:core:1.17.1")
    implementation 'com.google.ar.sceneform.ux:sceneform-ux:1.17.1'

    //firebase
    implementation 'com.google.firebase:firebase-messaging:21.1.0'
    implementation 'com.google.firebase:firebase-core:18.0.3'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.android.gms:play-services-base:17.6.0'
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'

    //rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    //concurrent
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'

    // Kotlin Coroutines
    def coroutines_version = '1.3.7'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.1.1"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-debug:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    //picasso
    implementation("com.squareup.picasso:picasso:2.71828") {
        exclude group: "com.android.support"
    }
    // glide
    implementation "com.github.bumptech.glide:glide:4.11.0"

    // viewpager
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    //ar vr vision
    implementation 'com.google.apis:google-api-services-vision:v1-rev16-1.22.0'
    implementation('com.google.api-client:google-api-client-android:1.22.0')
    implementation('com.google.http-client:google-http-client-gson:1.20.0')

    // ConcealerNestedScrollView & ConcealerRecyclerView
    // A library to make views hide from top and bottom while scrolling a custom NestedScrollView and\or a custom RecyclerView
    // https://github.com/SIMMORSAL/ConcealerNestedScrollView-ConcealerRecyclerView
    implementation 'com.simmorsal.library:concealer_nested_scroll_view:2.0.0'
    implementation 'com.dc.easyadapter:easyadapter:2.0.3'


    // billing lib
    implementation 'com.android.billingclient:billing:3.0.3'

    // Speech SDK
    implementation 'com.microsoft.cognitiveservices.speech:client-sdk:1.8.0'

    //biometric
    def biometric_version = '1.1.0'
    implementation "androidx.biometric:biometric:$biometric_version"

    implementation 'androidx.browser:browser:1.3.0'

    // https://github.com/stfalcon-studio/StfalconImageViewer
    implementation 'com.github.stfalcon:stfalcon-imageviewer:1.0.1'

    // Play Install Referrer Library
    // https://developer.android.com/google/play/installreferrer/library.html
    //implementation 'com.android.installreferrer:installreferrer:2.2'

    //crypto

    //https://github.com/str4d/ed25519-java
    implementation "net.i2p.crypto:eddsa:0.3.0"

    // Pure Java implementation of EdDSA-SHA3
    implementation "com.github.warchant:ed25519-sha3-java:2.0.1"
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
	arvifoxconf.extendsFrom implementation
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.ar.sceneform.plugin'

repositories {
    mavenCentral()
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/kotlindoc"
}


sceneform.asset('sampledata/model.fbx',
        'default',
        'sampledata/model.sfa',
        'src/main/res/raw/model')

task copyDeps(type: Copy) {
  from configurations.arvifoxconf
  into './depsdir'
}
